apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'project-report'

apply from: 'dependencies.gradle'

defaultTasks 'clean', 'check'

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.4.RELEASE")
    }
}

archivesBaseName='edison-jobtrigger'
group = 'de.otto.edison'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    // FIXME: pinning javassist to this version as there's a bug with powermock & javassist (3.20.0-GA) and Java 8 lambdas in later versions
    // be careful, javassist is a transitiv dependency for spring as well so this might cause trouble in the future
    resolutionStrategy.force "org.javassist:javassist:3.18.2-GA"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
    exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    compile libraries.edison
    compile libraries.bootstrap
    compile libraries.guava
    compile libraries.logging
    compile libraries.jetty
    compile libraries.asyncHttp
    compile libraries.gson

    testCompile libraries.test
}

test {
    // pass -Dspring.profiles.active=local and other system properties to the test task
    systemProperties = System.properties
}

tasks.processResources << {
    def resourcesDir = project.sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()

    def versionProperties = new File(resourcesDir, "version.properties")
    if (versionProperties) {
        def gitVersion = runCommand("git log -n 1 --format=format:%H")
        def fullVersion = fullyQualifiedVersion()
        versionProperties.text = "\ninfo.build.commit = ${gitVersion}" + "\ninfo.build.version = ${fullVersion}"
    }
}

def String fullyQualifiedVersion() {
    def env = System.getenv()
    def versionFromEnv = env.VERSION
    def version
    if (versionFromEnv) {
        version = versionFromEnv
    } else {
        version = '0.1-localbuild'
    }
    return version
}

def String runCommand(def command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    out.toString().trim()
}



